import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()

        // used for shadowjar
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = pluginGroup
    version = pluginVersion

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
        maven {
            // Spigot
            url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }
        maven {
            // VaultAPI
            url = 'https://jitpack.io'
        }
        maven {
            // WorldEdit
            url = 'https://maven.sk89q.com/repo/'
        }
        maven {
            // ProtocolLib
            url "https://repo.dmulloy2.net/nexus/repository/public/"
        }
        // PaperLib
        maven {
            url 'https://papermc.io/repo/repository/maven-public/'
        }
        // AnvilGUI
        maven {
            url 'https://repo.codemc.io/repository/maven-snapshots/'
        }
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
            filter ReplaceTokens, tokens: [version: version]
        }
    }
}

dependencies {
    shadow 'com.google.code.findbugs:jsr305:3.0.2'
    //shadow 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.14.2-R0.1-SNAPSHOT'
    //shadow 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
    shadow 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    shadow 'com.github.MilkBowl:VaultAPI:1.7'

    implementation 'net.wesjd:anvilgui:1.5.0-SNAPSHOT'
    implementation 'com.github.Mezy:BiomeMapping:master-SNAPSHOT'
    implementation "io.papermc:paperlib:1.0.5"
    
    // this should be on the server anyway
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.4.0'

    implementation project('Support-WorldEdit-6')
    implementation project('Support-WorldEdit-7')
}

shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]

    classifier = ''
    relocate 'io.papermc.lib', 'com.gmail.val59000mc.paperlib'
    minimize()
}

build.dependsOn(shadowJar)